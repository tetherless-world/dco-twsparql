PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX dco: <http://info.deepcarbon.net/schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>

#This query counts the number of countries whose organizations have publications on the DCO website
#Stephen Moon, July 2015
SELECT (concat("Total Publication Countries: ", (COUNT (DISTINCT ?countries))) as ?number) WHERE
{
{ ?publication a bibo:Article . } 
UNION { ?publication a bibo:Book . } 
UNION { ?publication a bibo:DocumentPart . } 
UNION { ?publication a dco:Poster . } 
UNION { ?publication a bibo:Thesis . }
?publication vivo:relatedBy ?relatedIDs.
?relatedIDs vivo:relates ?authors.
?authors vitro:mostSpecificType foaf:Person.
?authors dco:inOrganization ?organization.
?organization <http://purl.obolibrary.org/obo/RO_0001025> ?countries.
}
